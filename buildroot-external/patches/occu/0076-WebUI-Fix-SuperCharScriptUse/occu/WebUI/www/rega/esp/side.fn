function ::SetDP()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationDP( system.GetVar("value") ) );
      Write( '|' );
      Write( oTmp.ID() );
      Write( '|' );
      Write( system.GetVar("value") );
    }
  }
}

function ::SetValueAndDP()
{
  if( system.IsVar("id") && system.IsVar("value") && system.IsVar("dp") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( "SetValueAndDP|"#oTmp.ID()#"|" );
      string stValue = system.GetVar("value");
      string stDP = system.GetVar("dp");
      integer iposSV = stValue.Find("[SV]");
      if( iposSV == -1 )
      {
        Write( "NOSV|" );
        Write( oTmp.DestinationDP( stDP ) );
        object otDP = dom.GetObject( stDP );
        if( otDP )
        {
          Write( oTmp.DestinationValueType( otDP.ValueType() ) );
        }
        Write( oTmp.DestinationValue( stValue ) );
      }
      else
      {
        Write( "SV|"#stDP );
        Write( oTmp.DestinationDP( stDP ) );
        Write( oTmp.DestinationValueType( ivtSpecialValue ) );
        stValue = stValue.Substr(iposSV+4);
        Write( "|"#stValue );
        Write( oTmp.DestinationValue( stValue ) );
      }
    }
  }
}

function ::SetChannel()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationChannel( system.GetVar("value") ) );
    }
  }
}

function ::SetParam()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationParam( system.GetVar("value") ) );
    }
  }
}

function ::SetValueAndType()
{
  if( system.IsVar("id") && system.IsVar("value") && system.IsVar("type") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationValueType( system.GetVar("type") ) );
      Write( oTmp.DestinationValue( system.GetVar("value") ) );
    }
  }
}

function ::SetValue()
{
  string sCInfo = "SetValue: ";
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      var val;
      string sVal = system.GetVar("value");
      sCInfo = sCInfo#"Found value ["#sVal#"], ";
      string sUnit = "";
      if( system.IsVar("unit") )
      {
        sUnit = system.GetVar("unit");
        if( sUnit == "STRING" )
        {
          val = sVal.ToString();
        }
        else
        {
          sCInfo = sCInfo#"Found unit ["#sUnit#"], ";
          real fVal = system.GetVar("value").ToFloat();
          sCInfo = sCInfo#"Found real value ["#fVal#"], ";
          if( sUnit == "%" )
          {
            sCInfo = sCInfo#"div by 100, ";
            fVal = fVal / 100;
          }
          val = fVal;
        }
      }
      else
      {
        val = sVal.ToInteger();
        sCInfo = sCInfo#"ToInteger ["#val#"], ";
      }
      sCInfo = sCInfo#"Set ["#oTmp.Name()#"] to ["#val#"] ";
      if( oTmp.DestinationValue( val ) )
      {
        if( val == oTmp.DestinationValue() )
        {
          sCInfo = sCInfo#"OK";
        }
        else
        {
          sCInfo = sCInfo#"failed (Values are not equal ["#oTmp.DestinationValue()#"])";
        }
      }
      else
      {
        sCInfo = sCInfo#"failed (Failed to set DestinationValue)";
      }
    }
    else
    {
      sCInfo = sCInfo#"Failed to get object ["#system.GetVar("id")#"]";
    }
  }
  else
  {
    sCInfo = sCInfo#"Failed: Parameter missing";
  }
  sCInfo = sCInfo#".";
  Write( sCInfo );
  return;
}

function ::SetValueType()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationValueType( system.GetVar("value") ) );
    }
  }
}

function ::SetValueParam()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationValueParam( system.GetVar("value") ) );
    }
  }
}

function ::SetValueParamType()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      Write( oTmp.DestinationValueParamType( system.GetVar("value") ) );
    }
  }
}

function ::SetScript()
{
  if( system.IsVar("id") && system.IsVar("value") )
  {
    object oTmp = dom.GetObject( system.GetVar("id") );
    if( oTmp )
    {
      oTmp.DestinationValueType( ivtString );
      Write( oTmp.DestinationValue( system.GetVar("value").Replace("@$@%", "^") ) );
    }
  }
}

function ::ReloadSiDe()
{
  object oSD = dom.GetObject( system.GetVar("id") );
  if( oSD )
  {
    object oD = dom.GetObject( oSD.DestinationParent() );
    if( oD )
    {
      object oR = dom.GetObject( oD.DstParent() );
      #inc("/esp/side.inc");
    }
  }
  return;
}
